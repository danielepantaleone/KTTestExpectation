plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'io.github.danielepantaleone.kttestexpectation'
    compileSdk 34
    defaultConfig {
        minSdk 23
        versionName "1.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test:runner:1.5.2"
    testImplementation "androidx.test:core-ktx:1.5.0"
    testImplementation "junit:junit:4.13.2"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "io.github.danielepantaleone"
            artifactId = "kttestexpectation"
            version = "${android.defaultConfig.versionName}"
            afterEvaluate {
                from components.release
            }
            pom {
                name = "KTTestExpectation"
                description = "A simple Kotlin implementation of Swift's expectation"
                url = "https://github.com/danielepantaleone/KTTestExpectation"
                licenses {
                    license {
                        name = "MIT"
                        url = "https://github.com/danielepantaleone/KTTestExpectation/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "danielepantaleone"
                        name = "Daniele Pantaleone"
                        email = "danielepantaleone@me.com"
                    }
                }
                scm {
                    connection = "https://github.com/danielepantaleone/KTTestExpectation.git"
                    developerConnection = "https://github.com/danielepantaleone/KTTestExpectation.git"
                    url = "https://github.com/danielepantaleone/KTTestExpectation"
                }
            }
        }
    }
    repositories {
        maven {
            name = "MavenCentral"
            url = uri("https://s01.oss.sonatype.org/content/repositories/releases/")
            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_TOKEN")
            }
        }
    }
}